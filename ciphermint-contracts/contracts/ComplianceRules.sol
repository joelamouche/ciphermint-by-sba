// SPDX-License-Identifier: MIT
pragma solidity ^0.8.27;

import {FHE, ebool} from "@fhevm/solidity/lib/FHE.sol";
import {ZamaEthereumConfig} from "@fhevm/solidity/config/ZamaConfig.sol";
import {IIdentityRegistry} from "./IIdentityRegistry.sol";

// solhint-disable max-line-length
/**
 * @title ComplianceRules
 * @author Gustavo Valverde
 * @notice Checks age compliance using FHE operations
 * @dev Example for fhEVM Examples - Identity Category
 *
 * @custom:category identity
 * @custom:chapter compliance
 * @custom:concept Encrypted age verification for compliance checks
 * @custom:difficulty intermediate
 * @custom:depends-on IdentityRegistry,IIdentityRegistry,CompliantERC20
 * @custom:deploy-plan [{"contract":"IdentityRegistry","saveAs":"registry"},{"contract":"ComplianceRules","saveAs":"complianceRules","args":["@registry"]},{"contract":"CompliantERC20","saveAs":"token","args":["Compliant Token","CPL","@complianceRules"],"afterDeploy":["await complianceRules.setAuthorizedCaller(await token.getAddress(), true);","console.log(\"Authorized CompliantERC20 as compliance caller:\", await token.getAddress());"]}]
 *
 * This contract checks age compliance from IdentityRegistry and returns
 * encrypted boolean results. Consumer contracts (like CompliantERC20) can use
 * these results with FHE.select() for branch-free logic.
 *
 * Key patterns demonstrated:
 * 1. Encrypted age verification (isOver18)
 * 2. Integration with IdentityRegistry
 * 3. Encrypted result caching
 */
contract ComplianceRules is ZamaEthereumConfig {
    // solhint-enable max-line-length
    // ============ State ============

    /// @notice Reference to the identity registry
    IIdentityRegistry public immutable identityRegistry;

    /// @notice Owner/admin
    address public owner;
    /// @notice Pending owner for two-step ownership transfer
    address public pendingOwner;

    /// @notice Store last compliance check result for each user
    mapping(address user => ebool result) private complianceResults;

    /// @notice Authorized callers that can request compliance checks for others
    mapping(address caller => bool authorized) public authorizedCallers;

    // ============ Events ============

    /// @notice Emitted when a compliance check is performed for a user
    /// @param user Address of the user whose compliance was checked
    event ComplianceChecked(address indexed user);

    /// @notice Emitted when a caller's authorization is updated
    /// @param caller Address being authorized or revoked
    /// @param allowed Whether the caller is allowed
    event AuthorizedCallerUpdated(address indexed caller, bool indexed allowed);

    /// @notice Emitted when ownership transfer is initiated
    /// @param currentOwner Current owner address
    /// @param pendingOwner Address that can accept ownership
    event OwnershipTransferStarted(address indexed currentOwner, address indexed pendingOwner);

    /// @notice Emitted when ownership transfer is completed
    /// @param previousOwner Previous owner address
    /// @param newOwner New owner address
    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    // ============ Errors ============

    /// @notice Thrown when caller is not the contract owner
    error OnlyOwner();
    /// @notice Thrown when caller is not the pending owner
    error OnlyPendingOwner();
    /// @notice Thrown when new owner is the zero address
    error InvalidOwner();

    /// @notice Thrown when registry address is zero
    error RegistryNotSet();

    /// @notice Thrown when caller is not authorized to check another user
    error CallerNotAuthorized();

    /// @notice Thrown when caller lacks permission for encrypted result
    error AccessProhibited();

    // ============ Modifiers ============

    modifier onlyOwner() {
        if (msg.sender != owner) revert OnlyOwner();
        _;
    }

    modifier onlyAuthorizedOrSelf(address user) {
        if (msg.sender != user && !authorizedCallers[msg.sender]) {
            revert CallerNotAuthorized();
        }
        _;
    }

    // ============ Constructor ============

    /**
     * @notice Initialize with identity registry reference
     * @param registry Address of the IdentityRegistry contract
     */
    constructor(address registry) {
        if (registry == address(0)) revert RegistryNotSet();
        identityRegistry = IIdentityRegistry(registry);
        owner = msg.sender;
    }

    // ============ Admin Functions ============

    /**
     * @notice Allow or revoke a caller to check compliance for other users
     * @param caller Address to update
     * @param allowed Whether the caller is allowed
     */
    function setAuthorizedCaller(address caller, bool allowed) external onlyOwner {
        authorizedCallers[caller] = allowed;
        emit AuthorizedCallerUpdated(caller, allowed);
    }

    /**
     * @notice Initiate transfer of contract ownership
     * @param newOwner Address that can accept ownership
     */
    function transferOwnership(address newOwner) external onlyOwner {
        if (newOwner == address(0)) revert InvalidOwner();
        pendingOwner = newOwner;
        emit OwnershipTransferStarted(owner, newOwner);
    }

    /**
     * @notice Accept ownership transfer
     */
    function acceptOwnership() external {
        if (msg.sender != pendingOwner) revert OnlyPendingOwner();
        address previousOwner = owner;
        owner = pendingOwner;
        pendingOwner = address(0);
        emit OwnershipTransferred(previousOwner, owner);
    }

    // ============ Compliance Checks ============

    /**
     * @notice Check if user passes all compliance requirements
     * @dev Checks: isAttested AND isOver18
     * @param user Address to check
     * @return Encrypted boolean indicating compliance status
     *
     * Note: This function makes external calls to IdentityRegistry which
     * computes and stores verification results. The result is stored locally
     * for later retrieval.
     */
    function checkCompliance(address user) external onlyAuthorizedOrSelf(user) returns (ebool) {
        // Check if user is attested
        if (!identityRegistry.isAttested(user)) {
            ebool notAttestedResult = FHE.asEbool(false);
            FHE.allowThis(notAttestedResult);
            FHE.allow(notAttestedResult, msg.sender);
            complianceResults[user] = notAttestedResult;
            return notAttestedResult;
        }

        // Check if user is over 18
        ebool isOver18 = identityRegistry.isOver18(user);

        // Store and grant permissions
        complianceResults[user] = isOver18;
        FHE.allowThis(isOver18);
        FHE.allow(isOver18, msg.sender);

        emit ComplianceChecked(user);

        return isOver18;
    }

    /**
     * @notice Get the last compliance check result for a user
     * @dev Call checkCompliance first to compute and store the result
     * @param user Address to get result for
     * @return Encrypted boolean result
     */
    function getComplianceResult(address user) external view returns (ebool) {
        ebool result = complianceResults[user];
        if (!FHE.isSenderAllowed(result)) revert AccessProhibited();
        return result;
    }

    /**
     * @notice Check if compliance result exists for user
     * @param user Address to check
     * @return Whether a cached result exists
     */
    function hasComplianceResult(address user) external view returns (bool) {
        return FHE.isInitialized(complianceResults[user]);
    }
}
